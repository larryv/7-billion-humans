-- 7 Billion Humans (2215) --
-- 34: Seek and Destroy 1 --

-- INSTRUCTIONS: "Shred the smallest data cube in each column."

-- AVAILABLE COMMANDS: step, pickUp, drop, giveTo, nearest, set, if,
-- jump, end, comment

-- SIZE: 59 commands (challenge 9, shortest known 8)
-- BEST AVERAGE RUNTIME: 13 seconds (challenge 21, fastest known 8)


mem2 = nearest shredder
step n
step n
step n
step n
mem1 = set s
if c != datacube:
	-- 2nd easternmost worker
	if n < mem1:
		mem1 = set n
	endif
	step n
	step n
	step n
	if s < mem1:
		mem1 = set s
	endif
	if c < mem1:
		mem1 = set c
	endif
	if n < mem1:
		mem1 = set n
	endif
	step n
	step n
else:
	if c < mem1:
		mem1 = set c
	endif
	if n == datacube:
		-- 2nd westernmost worker
		if n < mem1:
			mem1 = set n
		endif
		step n
		step n
		step n
		if s < mem1:
			mem1 = set s
		endif
		if n < mem1:
			mem1 = set n
		endif
		step n
		step n
		if c < mem1:
			mem1 = set c
		endif
	else:
		step n
		step n
		step n
		if s < mem1:
			mem1 = set s
		endif
		if c == datacube:
			-- Westernmost worker
			if c < mem1:
				mem1 = set c
			endif
			step n
			step n
			if c < mem1:
				mem1 = set c
			endif
		else:
			-- Easternmost worker
			step n
			step n
			if s < mem1:
				mem1 = set s
			endif
		endif
	endif
endif
if n < mem1:
	mem1 = set n
endif
pickup mem1
giveto mem2


-- vim: set autoindent noexpandtab shiftwidth=8:
